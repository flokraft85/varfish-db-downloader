# Download dbSNP, normalize, and convert into TSV for import.


rule GRCh37_dbsnp_b155_download:
    output:
        vcf="GRCh37/dbSNP/b155/download/GCF_000001405.25.gz",
        tbi="GRCh37/dbSNP/b155/download/GCF_000001405.25.gz.tbi",
    log: "GRCh37/dbSNP/b155/download/GCF_000001405.25.gz.log"
    shell:
        r"""
        wget \
            -o {log} \
            -O {output.vcf} \
            ftp://ftp.ncbi.nih.gov/snp/latest_release/VCF/GCF_000001405.25.gz
        wget \
            -a {log} \
            -O {output.tbi} \
            ftp://ftp.ncbi.nih.gov/snp/latest_release/VCF/GCF_000001405.25.gz.tbi

        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        md5sum $(basename {output.tbi}) >$(basename {output.tbi}).md5
        """


rule GRCh37_dbsnp_b155_normalize:
    input:
        reference="GRCh37/reference/hs37d5/hs37d5.fa",
        vcf="GRCh37/dbSNP/b155/download/GCF_000001405.25.gz",
    output:
        vcf="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.vcf.gz",
        tbi="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.vcf.gz.tbi",
    shell:
        r"""
        bcftools norm \
            --threads 16 \
            --multiallelics - \
            --fasta-ref {input.reference} \
            -O z \
            -o {output.vcf} \
            {input.vcf}

        tabix -f {output.vcf}
        pushd $(dirname {output.vcf})
        md5sum $(basename {output.vcf}) >$(basename {output.vcf}).md5
        """


rule GRCh37_dbsnp_b155_split:
    input:
        vcf="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.vcf.gz",
        tbi="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.vcf.gz.tbi",
    output:
        gz="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.{chrom}.vcf.gz",
        tbi="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.{chrom}.vcf.gz.tbi"
    shell:
        r"""
        tabix -h {input.vcf} {wildcards.chrom} \
        | bgzip -c \
        > {output.gz}
        tabix {output.gz}
        """


rule grch37_dbsnp_b155_tsv:
    input:
        header="header/dbsnp.txt",
        vcf="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.{chrom}.vcf.gz",
        tbi="GRCh37/dbSNP/b155/download/GCF_000001405.25.normalized.{chrom}.vcf.gz.tbi",
    output:
        release_info="GRCh37/dbSNP/b155/Dbsnp.{chrom}.release_info",
        tsv="GRCh37/dbSNP/b155/Dbsnp.{chrom}.tsv",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query {input.vcf} \
                -f 'GRCh37\t%CHROM\t%POS\t%END\t\t%REF\t%ALT\t%ID\n' \
            | sort -u -t $'\t' -k 2,2 -k 3,3 -k 6,6 -k 7,7 -S 80%
        ) \
        | python tools/ucsc_binning.py \
        > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nDbsnp\tb155\tGRCh37\t" > {output.release_info}
        """