# Obtain "knownGeneAA" track from ENSEMBL and convert into TSV via VCF.


rule grch38_knowngeneaa_download:
    output:
        fa="GRCh38/knowngeneaa/latest/download/knownGene.exonAA.fa.gz",
    shell:
        r"""
        wget \
            -O {output.fa} \
            https://hgdownload.soe.ucsc.edu/goldenPath/hg38/multiz100way/alignments/knownGene.exonAA.fa.gz
        """


rule grch38_knowngeneaa_to_vcf:
    input:
        reference="GRCh38/reference/hs38/hs38.fa",
        fa="GRCh38/knowngeneaa/latest/download/knownGene.exonAA.fa.gz",
    output:
        vcf="GRCh38/knowngeneaa/latest/knownGeneAA.vcf.gz",
        tbi="GRCh38/knowngeneaa/latest/knownGeneAA.vcf.gz.tbi",
    shell:
        r"""
        python tools/knowngeneaa.py \
            {input.reference} \
            {input.fa} \
            --output /dev/stdout \
        | bcftools sort \
            -O z \
            -o {output.vcf}
        tabix -f {output.vcf}
        """


rule grch38_knowngeneaa_to_tsv:
    input:
        header="header/knowngeneaa.txt",
        vcf="GRCh38/knowngeneaa/latest/knownGeneAA.vcf.gz",
    output:
        tsv="GRCh38/knowngeneaa/latest/KnowngeneAA.tsv",
        release_info="GRCh38/knowngeneaa/latest/KnowngeneAA.release_info",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query \
                -f "GRCh38\t%CHROM\t%POS\t%END\t\t%UCSC_GENE\t\t%ALIGNMENT\n" \
                {input.vcf} \
            | awk -F $'\t' '
                BEGIN {{ OFS = FS }}
                {{
                    $7 = substr($6, 1, length($6) - 2)
                    print
                }}'
        ) \
        | python tools/ucsc_binning.py \
        > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nKnowngeneAA\t$(date +%Y/%m/%d)\tGRCh38\t" > {output.release_info}
        """