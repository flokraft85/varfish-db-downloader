rule GRCh37_helixmtdb_download:
    output:
        "GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.tsv",
    shell:
        r"""
        wget https://helix-research-public.s3.amazonaws.com/mito/HelixMTdb_20200327.tsv \
            -O {output}
        """


rule GRCh37_helixmtdb_vcf:
    input:
        "GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.tsv",
    output:
        "GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.splitted.vcf",
    run:
        import vcfpy
        import csv
        import json
        import math
        from collections import defaultdict


        # As stated in HelixMTdb paper by Bolze et al, 2019,
        # "Selective constraints and pathogenicity of mitochondrial DNA variants
        #  inferred from a novel database of 196,554 unrelated individuals."
        AN = 196_554

        #: Mitochondrial length
        MT_LENGTH = 16_569

        with open(input[0], "r") as fh:
            # discard first line and get header
            next(fh)
            header = next(fh).strip().split("\t")
            # construct VCF header
            vcf_header = vcfpy.Header(
                lines=[
                    vcfpy.HeaderLine("fileformat", "VCFv4.2"),
                    vcfpy.ContigHeaderLine.from_mapping({"ID": "MT", "length": MT_LENGTH}),
                ],
                samples=vcfpy.SamplesInfos(sample_names=[]),
            )
            vcf_header.add_info_line({
                "ID": "AC_HOM",
                "Number": "A",
                "Type": "Integer",
                "Description": "Homoplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "AN",
                "Number": "A",
                "Type": "Integer",
                "Description": "Allele number",
            })
            vcf_header.add_info_line({
                "ID": "AF",
                "Number": "A",
                "Type": "Float",
                "Description": "Allele frequency",
            })
            vcf_header.add_info_line({
                "ID": "AC_HET",
                "Number": "A",
                "Type": "Integer",
                "Description": "Heteroplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "AC_HET_MEAN",
                "Number": "A",
                "Type": "Float",
                "Description": "Mean of heteroplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "AC_HET_STDEV",
                "Number": "A",
                "Type": "Float",
                "Description": "Standard deviation of heteroplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "AC_HET_MIN",
                "Number": "A",
                "Type": "Float",
                "Description": "Min value of heteroplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "AC_HET_MAX",
                "Number": "A",
                "Type": "Float",
                "Description": "Max value of heteroplasmy allele count",
            })
            vcf_header.add_info_line({
                "ID": "TRIALLELIC",
                "Number": "0",
                "Type": "Flag",
                "Description": "Site is triallelic",
            })
            vcf_writer = vcfpy.Writer.from_path(
                output[0],
                vcf_header,
            )
            variants = defaultdict(
                lambda: {
                    "reference": "",
                    "alternative": "",
                    "ac_het": 0,
                    "ac_hom": 0,
                    "ac_het_mean": None,
                    "ac_het_stdev": None,
                    "ac_het_min": None,
                    "ac_het_max": None,
                    "triallelic": False,
                    "updates": -1,
                }
            )
            for line in fh:
                record = dict(zip(header, line.strip("\n").split("\t")))
                het_stats = json.loads(record["HET_STATS"])
                alts = record["ALT"].split(",")
                for alt in alts:
                    if len(record["REF"]) == 1 and len(alt) == 1:
                        var_type = vcfpy.SNV
                    elif len(record["REF"]) == len(alt):
                        var_type = vcfpy.MNV
                    else:
                        var_type = vcfpy.INDEL
                    signature = "{:05d}-{}-{}".format(int(record["POS"]), record["REF"], alt)
                    variants[signature]["reference"] = record["REF"]
                    variants[signature]["alternative"] = vcfpy.Substitution(var_type, alt)
                    variants[signature]["position"] = int(record["POS"])
                    variants[signature]["ac_het"] += math.ceil(int(record["COUNT_HET"]) / len(alts))
                    variants[signature]["ac_hom"] += int(record["COUNT_HOM"])
                    variants[signature]["updates"] += 1
                    variants[signature]["triallelic"] = len(alts) > 1
                    if variants[signature]["updates"] > 0:
                        variants[signature]["ac_het_mean"] = None
                        variants[signature]["ac_het_stdev"] = None
                        variants[signature]["ac_het_min"] = None
                        variants[signature]["ac_het_max"] = None
                    else:
                        variants[signature]["ac_het_mean"] = None if het_stats["mean"] == "NaN" else het_stats["mean"]
                        variants[signature]["ac_het_stdev"] = None if het_stats["stdev"] == "NaN" else het_stats["stdev"]
                        variants[signature]["ac_het_min"] = het_stats["min"]
                        variants[signature]["ac_het_max"] = het_stats["max"]
            for _, record in sorted(variants.items()):
                info = {
                    "AN": [AN],
                    "AF": [(record["ac_het"] + record["ac_hom"]) / AN],
                    "AC_HOM": [record["ac_hom"]],
                    "AC_HET": [record["ac_het"]],
                    "AC_HET_MEAN": [record["ac_het_mean"]],
                    "AC_HET_STDEV": [record["ac_het_stdev"]],
                    "AC_HET_MIN": [record["ac_het_min"]],
                    "AC_HET_MAX": [record["ac_het_max"]],
                }
                if record["updates"] > 0 or record["triallelic"]:
                    info["TRIALLELIC"] = True
                vcf_writer.write_record(
                    vcfpy.Record(
                        "MT",
                        int(record["position"]),
                        [],
                        record["reference"],
                        [record["alternative"]],
                        None,
                        [],
                        info
                    )
                )
            vcf_writer.close()



rule GRCh37_helixmtdb_normalize:
    input:
        vcf="GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.splitted.vcf",
        ref="GRCh37/reference/hs37d5/hs37d5.fa",
    output:
        "GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.splitted.normalized.vcf",
    shell:
        r"""
        bcftools norm \
            -m -any \
            -c w \
            -f {input.ref} \
            -o {output} \
            {input.vcf}
        """


rule GRCh37_helixmtdb_tsv:
    input:
        txt="GRCh37/HelixMTdb/20200327/download/HelixMTdb_20200327.splitted.normalized.vcf",
        header="header/helixmtdb.txt",
    output:
        tsv="GRCh37/HelixMTdb/20200327/HelixMtDb.tsv",
        release_info="GRCh37/HelixMTdb/20200327/HelixMtDb.release_info",
    shell:
        r"""
        (
            cat {input.header} | tr '\n' '\t' | sed -e 's/\t*$/\n/g';
            bcftools query \
                -f "GRCh37\t%CHROM\t%POS\t%END\t\t%REF\t%ALT\t%AC_HOM\t%AN\t%AF\t%AC_HET\t%AC_HET_MEAN\t%AC_HET_STDEV\t%AC_HET_MIN\t%AC_HET_MAX\t%TRIALLELIC\n" \
                {input.txt} \
            | awk -F$'\t' 'BEGIN{{OFS=FS}}{{if($16=="."){{$16=0}}print}}'
        ) \
        | python tools/ucsc_binning.py \
        > {output.tsv}

        echo -e "table\tversion\tgenomebuild\tnull_value\nHelixMtDb\t20200327\tGRCh37\t." > {output.release_info}
        """